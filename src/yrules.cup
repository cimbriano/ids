import java_cup.runtime.*;
import ids.*;

parser code {:

    public void report_error(String msg, Object i) {
	StringBuffer m = new StringBuffer("Error");

	if (i instanceof java_cup.runtime.Symbol) {
	    java_cup.runtime.Symbol s = ((java_cup.runtime.Symbol) i);

	    if (s.left >= 0) {
		m.append(" in line " + (s.left+1));

		if (s.right >= 0)
		    m.append(", column " + (s.right+1));

		m.append(", symbol " + s.toString());
		m.append(", value " + s.value.toString());
	    }
	}

	m.append(" : " + msg);

	System.err.println(m);
    }

    public void report_fatal_error(String msg, Object i) {
	report_error(msg, i);
	System.exit(1);
    }

    public void check_flags(String flags) {
	for (int x = 0; x < flags.length(); x++)
	    switch (flags.charAt(x)) {
	    case 'S':
	    case 'A':
	    case 'F':
	    case 'R':
	    case 'P':
	    case 'U':
		break;
	    default:
		report_fatal_error("Malformed flags", flags);
	    }
    }
    
:};

/* */

terminal String STRING, IP, REGEXP;
terminal Integer PORT;

terminal ANY, SET_IP, SET_TYPE, RECV, SEND, SET_PROTO, TCP, UDP, WITH_FLAGS;
terminal SET_HOST, NLINE, SET_NAME, TCP_STREAM, PROTOCOL, SET_SRC, SET_DST;

/*
non terminal Object file, rule_list, host, rule, tcp_stream_rule, proto;
non terminal Object protocol_rule, sub_rule, port, ip, direction;
non terminal Object sub_rules;
*/

non terminal ThreatDefinition file;
non terminal AbstractRuleList rule_list;
non terminal AbstractRule rule;

non terminal TcpRule tcp_stream_rule;

non terminal ProtocolRule protocol_rule;
non terminal ProtocolSubrule sub_rule;
non terminal ProtocolSubruleList sub_rules;

non terminal String ip, direction, proto, host;
non terminal Integer port;

/* */

file ::= host rule_list;

rule_list ::= rule | rule_list rule;

host ::= SET_HOST IP NLINE NLINE;

rule ::= SET_NAME STRING NLINE
         tcp_stream_rule NLINE
         |
         SET_NAME STRING NLINE
         protocol_rule NLINE;

tcp_stream_rule ::= SET_TYPE TCP_STREAM NLINE
                    SET_SRC port NLINE
                    SET_DST port NLINE
                    SET_IP ip NLINE 
                    direction REGEXP NLINE;

protocol_rule ::= SET_TYPE PROTOCOL NLINE
                  SET_PROTO proto : p NLINE
                  SET_SRC port : sp NLINE
                  SET_DST port : dp NLINE
                  SET_IP ip : ip NLINE
                  sub_rules : l
		  {: RESULT = new ProtocolRule(p.equals("TCP") ? true : false, sp, dp, ip, l); :};

sub_rules ::= sub_rule : r {: RESULT = new ProtocolSubruleList(r); :}
              |
	      sub_rules : l sub_rule : r 
	      {: l.add(r); RESULT = l; :};

sub_rule ::= direction : d REGEXP : r NLINE
             {: RESULT = new ProtocolSubrule(d.equals("SEND") ? true : false, r, ""); :}
             |
             direction : d REGEXP : r WITH_FLAGS STRING : f NLINE
             {: parser.check_flags(f);
                RESULT = new ProtocolSubrule(d.equals("SEND") ? true : false, r, ""); :}
             |
             direction : d REGEXP : r WITH_FLAGS NLINE
             {: RESULT = new ProtocolSubrule(d.equals("SEND") ? true : false, r, ""); :};

proto ::= TCP
          {: RESULT = new String("TCP"); :}
          |
          UDP
          {: RESULT = new String("UDP"); :};

direction ::= RECV
              {: RESULT = new String("RECV"); :}
              |
	      SEND
              {: RESULT = new String("SEND"); :};

ip ::= IP : ip
       {: RESULT = new String(ip); :}
       |
       ANY
       {: RESULT = new String("*.*.*.*"); :};

port ::= PORT : p
         {: RESULT = new Integer(p.intValue()); :}
         |
	 ANY
         {: RESULT = new Integer(0); :};